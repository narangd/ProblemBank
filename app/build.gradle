apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
//    buildToolsVersion '28.0.3'

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            versionCode code
        }
    } else {
        throw new Exception("Could not read version.properties!")
    }

    defaultConfig {
        applicationId "person.sykim.problembank"
        minSdkVersion 21
        targetSdkVersion 28
//        versionCode 1
        versionName "1.0.${versionCode}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    dataBinding {
        enabled = true
    }
}

def versionLombok = '1.16.20' // fixed. for bugs.

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-alpha02'

    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-database:16.1.0'
    implementation 'com.google.firebase:firebase-config:16.4.1'
    implementation 'com.google.firebase:firebase-auth:16.2.0'
    implementation 'com.google.firebase:firebase-messaging:17.5.0'

    // Firebase-UI
    implementation 'com.firebaseui:firebase-ui-auth:4.0.0'

    // Google
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    // BindView
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'com.jakewharton:butterknife:10.1.0'

    // Lombok
    compileOnly "org.projectlombok:lombok:$versionLombok"
    annotationProcessor "org.projectlombok:lombok:$versionLombok"

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.jsoup:jsoup:1.11.3'

    // Preference
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
}

apply plugin: 'com.google.gms.google-services'
